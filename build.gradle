buildscript {
	ext {
		springBootVersion = '2.0.3.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'

group = 'com.thinven'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

ext {
	querydslVersion = "4.1.4"
	swagger2Version = "2.8.0"
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-aop') 
	compile("org.springframework.boot:spring-boot-starter-security")
	compile('org.springframework.boot:spring-boot-starter-cache')
	
	compile group: 'net.sf.ehcache', name: 'ehcache', version: '2.10.3'
	
	providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	
	compile("io.springfox:springfox-swagger2:${swagger2Version}")
	compile("io.springfox:springfox-swagger-ui:${swagger2Version}")
	compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.2.5'

	compile("com.querydsl:querydsl-jpa:${querydslVersion}")
	compile("com.querydsl:querydsl-apt:${querydslVersion}")
	compile("com.querydsl:querydsl-core:${querydslVersion}")
	
	compile("org.springframework:spring-context-support:5.0.7.RELEASE")
	compile("javax.mail:mail:1.5.0-b01")
	compile("org.apache.commons:commons-lang3:3.7")
	compile("commons-configuration:commons-configuration:1.10")
	compile("com.google.code.gson:gson:2.8.5")

	compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'

}

configurations {
	querydslapt
}

sourceSets {
	generated {
		java {
			srcDirs = ['src/main/generated']
		}
	}
}

task generateQueryDSL(type: JavaCompile, group: 'build') {
	source = sourceSets.main.java
	classpath = configurations.compile + configurations.querydslapt
	options.compilerArgs = [
		"-proc:only",
		"-processor",
		"com.querydsl.apt.jpa.JPAAnnotationProcessor"
	]
	options.encoding = 'UTF-8'
	destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
	dependsOn generateQueryDSL
	source generateQueryDSL.destinationDir
}

compileGeneratedJava {
	dependsOn generateQueryDSL
	options.warnings = false
	classpath += sourceSets.main.runtimeClasspath
}

clean {
	delete sourceSets.generated.java.srcDirs.iterator().next()
}
